#!/usr/bin/env -S just --working-directory . --justfile
set quiet

font_dir := home_directory() + "/.local/share/fonts"
version := "v3.4.0"

fonts := '''
  0xProto
  3270
  Agave
  AnonymousPro
  Arimo
  AurulentSansMono
  BigBlueTerminal
  BitstreamVeraSansMono
  CascadiaCode
  CascadiaMono
  CodeNewRoman
  ComicShannsMono
  CommitMono
  Cousine
  D2Coding
  DaddyTimeMono
  DejaVuSansMono
  DroidSansMono
  EnvyCodeR
  FantasqueSansMono
  FiraCode
  FiraMono
  FontPatcher
  GeistMono
  Go-Mono
  Gohu
  Hack
  Hasklig
  HeavyData
  Hermit
  iA-Writer
  IBMPlexMono
  Inconsolata
  InconsolataGo
  InconsolataLGC
  IntelOneMono
  Iosevka
  IosevkaTerm
  IosevkaTermSlab
  JetBrainsMono
  Lekton
  LiberationMono
  Lilex
  MartianMono
  Meslo
  Monaspace
  Monofur
  Monoid
  Mononoki
  MPlus
  NerdFontsSymbolsOnly
  Noto
  OpenDyslexic
  Overpass
  ProFont
  ProggyClean
  RobotoMono
  ShareTechMono
  SourceCodePro
  SpaceMono
  Terminus
  Tinos
  Ubuntu
  UbuntuMono
  VictorMono
'''

[private]
default:
  echo "nerdfont installer\n"
  just --justfile={{justfile()}} --list

# Install the specified nerdfont
install font:
  #!/usr/bin/env bash
  set -e
  # Create the font folder if it doesn't exist
  test -d "{{font_dir}}" || mkdir -p "{{font_dir}}"
  # Download the font
  wget --quiet "https://github.com/ryanoasis/nerd-fonts/releases/download/{{version}}/{{font}}.zip" -O "/tmp/{{font}}.zip"
  unzip -u -q -o "/tmp/{{font}}.zip" -d "{{font_dir}}"
  rm "/tmp/{{font}}.zip"
  fc-cache -f >/dev/null
  echo "{{font}} installed"

# List all the available fonts
list:
  echo -n "{{fonts}}"
 
# Check the latest version of nerdfonts
latest-version:
  echo "Latest version is $(git ls-remote --tags https://github.com/ryanoasis/nerd-fonts.git | awk -F/ '{print $3}' | sed 's/\^{}//' | sort -V | tail -n1)"
  echo "Currently using version {{version}}"

