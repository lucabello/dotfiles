#!/usr/bin/env -S just --working-directory . --justfile
set quiet

hledger := `which hledger`  # require 'hledger'
ledger_budget_file := parent_directory(env('LEDGER_FILE')) + "/budgets.journal"
budget_opts := "--file=" + ledger_budget_file + " -V --auto"

[private]
default:
  echo "Set of utilities to display budgeting information with HLedger\n"
  just --justfile {{justfile()}} --list

# Display the monthly balance from three months ago to today
[positional-arguments]
monthly:
  hledger bs assets:budget {{budget_opts}} \
    --monthly \
    --forecast="from 2023/08 to today" \
    --period="from 4 months ago to today" \
    "$@"

# Display the current balance for all budgets
[positional-arguments]
print:
  echo "Budget balance:"
  hledger bal assets:budget {{budget_opts}} \
    --forecast="from 2023/08 to today" \
    "$@"

# Display the current cash balance next to the budget balance
compare:
  echo "Current Commerzbank balance:"
  hledger bal expr:"credit-card OR commerzbank:checking"
  echo "\nCurrent Budget balance:"
  hledger bal assets:budget {{budget_opts}} \
    --forecast="from 2023/08 to today" \
    --depth=2

# Display the monthly budget allocation
allocation:
  echo "Current budget allocation:"
  cat {{ledger_budget_file}} | grep "~ monthly in 2025" --after-context=11
