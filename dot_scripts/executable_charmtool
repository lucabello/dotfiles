#!/usr/bin/env -S just --working-directory . --justfile
# set positional-arguments
# set export
set quiet

set shell := ["bash", "-uc"]

[private]
default:
  echo "Utility operations for Observability(core) charms\n"
  just --justfile {{justfile()}} --list

[group('charm'), doc('Close a track')]
close-track charm track:
  charmcraft close {{charm}} {{track}}/edge
  charmcraft close {{charm}} {{track}}/beta
  charmcraft close {{charm}} {{track}}/candidate
  charmcraft close {{charm}} {{track}}/stable

[group('charm'), doc('Set the default track')]
set-default-track charm track:
  #!/usr/bin/env bash
  # Verify CHARMCRAFT_AUTH is specified
  if [[ -z "$CHARMCRAFT_AUTH" ]]; then
    echo "You must set the CHARMCRAFT_AUTH environment variable. Please run:"
    echo '  charmcraft login --export ~/.charmcraft.auth && export CHARMCRAFT_AUTH="$(cat ~/.charmcraft.auth)"'
    exit 1
  fi
  # Prepare the authentication header
  CHARMHUB_MACAROON_HEADER="Authorization: Macaroon $(echo "$CHARMCRAFT_AUTH" | base64 -d | jq -r .v)"
  # Send the request to change the default track
  curl -X PATCH "https://api.charmhub.io/v1/charm/{{charm}}" \
    -H "$CHARMHUB_MACAROON_HEADER" \
    -H "Content-Type: application/json" \
    -d '{"default-track": "{{track}}"}'

[group("dev"), doc("Delete all the LXC instances that charmcraft creates to pack charms")]
[arg("project", long="project", pattern="^(charmcraft|rockcraft)$", help="The LXC project that owns the instances")]
[arg("last_used", long="last-used", help="Delete instances that haven't been used since the given time")]
[arg("dry_run", long="dry-run", value="true", help="Perform a dry run (don't delete instances)")]
lxc-purge project="charmcraft" last_used="-7 days" dry_run="false":
  #!/usr/bin/env bash
  target_date="$(date -d '{{last_used}}' +'%F')"
  jq_selection_expr=""
  lxc_instances="$(lxc list --project={{project}} --format=json | jq --arg cutoff "$target_date" '[.[] | select(.last_used_at | . < $cutoff)]')"
  lxc_instances_count="$(echo "$lxc_instances" | jq -r '[ .[] | select(.name | startswith("{{project}}-")) | .name ] | length')"
  echo "${lxc_instances_count} lxc containers will be removed (not used since {{last_used}})"
  if [[ "{{dry_run}}" == "true" ]]; then echo "This is a dry run. No container will actually be removed."; fi
  read -p "Confirm the deletion? (y/n): " confirmation
  if [[ ! "$confirmation" =~ ^[Yy]$ ]]; then echo "Operation cancelled." && exit 0; fi
  for instance in $(echo $lxc_instances | jq -r '.[] | select(.name | startswith("{{project}}-")) | .name'); do
    echo "Deleting ${instance}"
    if [[ "{{dry_run}}" == "false" ]]; then
      lxc --project={{project}} delete "$instance"
    fi
  done

[group("dev"), doc("Get the charm resources as flags from the charmcraft.yaml file")]
charm-resources:
  if [[ ! -f charmcraft.yaml ]]; then echo "charmcraft.yaml not found" && exit 1; fi
  echo -n $(yq eval ".resources | to_entries | map(select(.value.upstream-source != null) | \"--resource \" + .key + \"=\" + .value.upstream-source) | join (\" \")" charmcraft.yaml)'
