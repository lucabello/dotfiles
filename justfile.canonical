microk8s-channel := '1.30-strict/stable'
juju-channel := '3.5/stable'
ipaddr := `ip -4 -j route get 2.2.2.2 | jq -r '.[] | .prefsrc'`

install-canonical: install-microk8s install-lxd install-juju install-dev

[group('install')]
[doc('Install MicroK8s')]
install-microk8s:
    sudo snap install microk8s --channel={{microk8s-channel}}
    sudo snap alias microk8s.kubectl kubectl
    mkdir -p "$HOME"/.kube
    sudo usermod -aG snap_microk8s $USER
    exec sg snap_microk8s "microk8s config > ~/.kube/config && sudo chown -f -R $USER ~/.kube"
    exec sg snap_microk8s "microk8s status --wait-ready"
    exec sg snap_microk8s "sudo microk8s enable dns hostpath-storage ingress rbac && microk8s status --wait-ready"
    exec sg snap_microk8s "sudo microk8s enable metallb:{{ipaddr}}-{{ipaddr}} && microk8s status --wait-ready"
    exec sg snap_microk8s "sudo microk8s enable registry --wait-ready"

[group('install')]
[doc('Install LXD')]
install-lxd:
    sudo snap install lxd && sudo usermod -aG lxd $USER
    exec sg lxd "lxd init --auto"

[group('install')]
[doc('Install Juju and Craft-tools')]
install-juju:
    sudo snap install juju --channel={{juju-channel}}
    sudo snap install rockcraft --classic
    sudo snap install charmcraft --classic

[group('install')]
[doc('Install developer tools')]
install-dev:
    if ! which tox; then uv tool install tox; fi
    sudo snap install jhack && sudo snap connect jhack:dot-local-share-juju snapd 
    cargo install --git https://github.com/sed-i/juju-graph
    # go install github.com/snapcore/spread/cmd/spread@latest
    curl -fsSL https://goss.rocks/install | sudo sh
    # Install GH and GH Dashboard
    if ! which gh; then \
        sudo curl -fsSLo /etc/apt/keyrings/githubcli-archive-keyring.gpg \
        https://cli.github.com/packages/githubcli-archive-keyring.gpg; \
        echo "deb [arch=$(dpkg --print-architecture) \
        signed-by=/etc/apt/keyrings/githubcli-archive-keyring.gpg] \
        https://cli.github.com/packages stable main" | \
        sudo tee /etc/apt/sources.list.d/github-cli.list > /dev/null; \
        sudo apt update && sudo apt install gh -y; \
    fi
    gh auth login
    gh extension install dlvhdr/gh-dash
